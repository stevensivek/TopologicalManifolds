/-
Copyright (c) 2025 Steven Sivek. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Steven Sivek
-/
import Mathlib.Tactic
import Mathlib.Geometry.Manifold.IsManifold.InteriorBoundary

/-!
# Interiors and boundaries

In this file we prove theorems about the interiors and boundaries of charted spaces,
assuming that the underlying Euclidean spaces have invariance of domain.  Separately
we will prove invariance of domain for â„Â¹ = `EuclideanSpace â„ (Fin 1)`, establishing
that 1-manifolds with boundary have closed boundary.

## Main definitions

- `HasInvarianceOfDomain` : a topological space `X` belongs to this class if any
  continuous `f âˆˆ PartialEquiv X X` sends neighborhoods of points `x âˆˆ f.source`
  to neighborhoods of `f x`.

## Main results

In each of these results we assume that the spaces in question are all
modeled on `I : ModelWithCorners ğ•œ E H` satisfying `HasInvarianceOfDomain E`.

- `isInteriorPoint_iff_any_chart` : a point `x âˆˆ M` is an interior point iff
  `I (f x)` is in the interior of `range I` for *every* `f : PartialHomeomorph M H`
  whose source contains `x`.  (By definition, `isInteriorPoint` only requires this
  for the specific chart `f = chartAt H x`.)
- `isBoundaryPoint_iff_any_chart` : a point `x âˆˆ M` is a boundary point iff
  `I (f x)` is in the frontier of `range I` for *every* `f : PartialHomeomorph M H`
  whose source contains x.  (By definition, `isBoundaryPoint` only requires this
  for the specific chart `f = chartAt H x`.)
- `isOpen_interior` : `I.interior M` is open.
- `dense_interior`: `I.interior M` is dense in M.
- `isNonempty_interior` : if `M` is nonempty then `I.interior M` is nonempty.
- `isClosed_boundary` : `I.boundary M` is closed.
- `isNowhereDense_boundary` : `I.boundary M` is nowhere dense.
- `homeomorph_boundary`: any homeomorphism `Ï† : X â‰ƒâ‚œ Y` produces a homeomorphism
  `I.boundary X â‰ƒâ‚œ I.boundary Y`.
- `homeomorph_interior`: any homeomorphism `Ï† : X â‰ƒâ‚œ Y` produces a homeomorphism
  `I.interior X â‰ƒâ‚œ I.interior Y`.
- `instBoundarylessManifold_interior`: `I.interior Y` is a `BoundarylessManifold`.
-/

open Set Function Topology TopologicalSpace

namespace InvarianceOfDomain

class HasInvarianceOfDomain (X : Type*) [TopologicalSpace X] : Prop where
  invariance_of_domain {x : X} {s : Set X} {f : PartialEquiv X X}
                       (hCont : ContinuousOn f f.source) :
    s âˆˆ nhds x â†’ s âŠ† f.source â†’ f '' s âˆˆ nhds (f x)

theorem maps_nhds_to_nhds (X : Type*) [TopologicalSpace X]
    [instID : HasInvarianceOfDomain X] {x : X} {s : Set X} {f : PartialEquiv X X}
    (hCont : ContinuousOn f f.source) :
    s âˆˆ nhds x â†’ s âŠ† f.source â†’ f '' s âˆˆ nhds (f x) := by
  exact instID.invariance_of_domain hCont

/-
  The following theorems prove that invariance of domain is preserved under
  homeomorphism.  Elsewhere we will prove that â„ has invariance of domain,
  and then use this to deduce the same for `EuclideanSpace â„ (Fin 1)`.
-/

/-- Invariance of domain is preserved under homeomorphisms. -/
theorem maps_nhds_to_nhds_from_homeomorph
    {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [HasInvarianceOfDomain Y]
    (Ï† : Homeomorph X Y) {x : X} {s : Set X} {f : PartialEquiv X X}
    (hfCont : ContinuousOn f f.source) :
    s âˆˆ nhds x â†’ s âŠ† f.source â†’ f '' s âˆˆ nhds (f x) := by
  intro h hfSource

  let x' : Y := Ï† x
  let s' : Set Y := Ï† '' s
  let f' : PartialEquiv Y Y :=
    Ï†.symm.toPartialEquiv.trans (f.trans Ï†.toPartialEquiv)

  have h' : s' âˆˆ nhds x' := by
    apply mem_interior_iff_mem_nhds.mp
    rw [â† Ï†.image_interior]
    exact mem_image_of_mem (â‡‘Ï†) (mem_interior_iff_mem_nhds.mpr h)

  have hfSource' : s' âŠ† f'.source := by
    let Ï†inv := Ï†.symm.toPartialEquiv
    let fÏ† := f.trans Ï†.toPartialEquiv
    have hâ‚ : f'.source = Ï†inv â»Â¹' fÏ†.source := by
      rw [â† univ_inter (Ï†inv â»Â¹' fÏ†.source), show univ = Ï†inv.source by rfl]
      exact PartialEquiv.trans_source Ï†inv fÏ†
    have hâ‚‚ : fÏ†.source = f.source := by
      exact inter_eq_self_of_subset_left (by exact fun _ _ â†¦ trivial)
    rw [hâ‚, hâ‚‚]
    apply image_subset_iff.mp
    have : Ï†.symm '' s' = Ï† â»Â¹' s' := by
      ext a
      simp only [mem_preimage]
      constructor <;> intro ha
      Â· obtain âŸ¨b, _, hÏ†bâŸ© := ha
        rwa [â† show b = Ï† a by rw [â† Ï†.apply_symm_apply b]; exact congrArg Ï† hÏ†b]
      Â· rw [â† Ï†.symm_apply_apply a]
        exact mem_image_of_mem Ï†.symm ha
    rwa [show Ï†inv '' s' = Ï†.symm '' s' by rfl, this, Ï†.preimage_image s]

  have hfCont' : ContinuousOn f' f'.source := by
    have hCont : ContinuousOn (f.trans Ï†.toPartialEquiv) f.source := by
      rw [â† show Ï†.toPartialEquiv âˆ˜ f = f.trans Ï†.toPartialEquiv by exact rfl]
      apply Continuous.comp_continuousOn Ï†.continuous hfCont
    have : f.source = (â†‘Ï†.symm.toPartialEquiv '' f'.source) := by
      apply (preimage_eq_iff_eq_image ?_).mp ?_
      Â· exact Ï†.symm.bijective
      Â· simp_all only [PartialEquiv.trans_source, Equiv.toPartialEquiv_source,
          Equiv.toPartialEquiv_apply, Homeomorph.coe_toEquiv, preimage_univ, inter_univ,
          univ_inter, PartialEquiv.coe_trans, s', f']
    rw [this] at hCont
    rw [show f' = (f.trans Ï†.toPartialEquiv) âˆ˜ Ï†.symm.toPartialEquiv by exact rfl]
    exact ContinuousOn.image_comp_continuous hCont Ï†.continuous_symm

  have hÏ†_symm_comp_f' (y : X) : Ï†.symm (f' (Ï† y)) = f y := by
    rw [â† Ï†.symm_apply_apply (f y)]
    apply congrArg Ï†.symm
    calc
      f' (Ï† y) = (f.trans Ï†.toPartialEquiv) (Ï†.symm (Ï† y)) := by rfl
      _ = (f.trans Ï†.toPartialEquiv) y := by
        exact congrArg (f.trans Ï†.toPartialEquiv) (Ï†.symm_apply_apply y)
      _ = Ï† (f y) := by rfl

  have hÏ†fs : Ï† '' (f '' s) = (f') '' s' := by
    ext y
    constructor <;> rintro âŸ¨z, âŸ¨w, hws, hwzâŸ©, hzyâŸ©
    Â· rw [â† hzy, â† hwz, â† Ï†.symm_apply_apply w]
      apply mem_image_of_mem
      exact mem_image_of_mem Ï† hws
    Â· rw [â† hzy, â† hwz, (EquivLike.inv_apply_eq (e := Ï†)).mp (hÏ†_symm_comp_f' w)]
      apply mem_image_of_mem
      exact mem_image_of_mem f hws

  have : (f '' s) âˆˆ nhds (f x) â†” ((f') '' s') âˆˆ nhds (f' x') := by
    rw [â† hÏ†fs, Ï†.image_eq_preimage (f '' s)]
    have : Filter.map Ï†.symm (nhds (f' x')) = nhds (f x) := by
      rw [â† hÏ†_symm_comp_f' x]
      exact Ï†.symm.map_nhds_eq (f' x')
    rw [â† this]
    rfl
  apply this.mpr
  exact maps_nhds_to_nhds Y hfCont' h' hfSource'

def HasInvarianceOfDomain_from_homeomorph
    {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [HasInvarianceOfDomain Y] (Ï† : Homeomorph X Y) :
    HasInvarianceOfDomain X where
  invariance_of_domain := by exact maps_nhds_to_nhds_from_homeomorph Ï†

end InvarianceOfDomain

namespace ModelWithCorners

open InvarianceOfDomain

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ğ•œ E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

omit [ChartedSpace H M] in
variable (I) (M) in
/-- If some `PartialHomeomorph M H` sends a point to the interior of `range I`, then
so does any other `PartialHomeomorph M H`. -/
theorem independence_of_interior [HasInvarianceOfDomain E] {x : M}
    {f g : PartialHomeomorph M H} (hfSource : x âˆˆ f.source) (hgSource : x âˆˆ g.source) :
    I (f x) âˆˆ interior (range I) â†’ I (g x) âˆˆ interior (range I) := by
  intro hfInterior
  apply mem_interior_iff_mem_nhds.mp at hfInterior
  apply mem_interior_iff_mem_nhds.mpr

  let f' : PartialEquiv M E := f.toPartialEquiv.trans I.toPartialEquiv
  have hf'symmCont : ContinuousOn f'.symm f'.target := by
    apply ContinuousOn.comp (PartialHomeomorph.continuousOn f.symm) ?_ ?_
    Â· simp only [PartialEquiv.restr_coe_symm, toPartialEquiv_coe_symm]
      exact continuousOn_symm I
    Â· simp only [PartialEquiv.symm_source, PartialEquiv.restr_coe_symm,
        toPartialEquiv_coe_symm, PartialHomeomorph.symm_toPartialEquiv]
      simp only [f', PartialEquiv.trans_target, target_eq, toPartialEquiv_coe_symm]
      exact fun _ hy â†¦ mem_preimage.mp <| mem_of_mem_inter_right hy

  let Uâ‚€ : Set M := f.source âˆ© g.source
  have hUâ‚€nhds : Uâ‚€ âˆˆ nhds x := by
    apply IsOpen.mem_nhds (IsOpen.inter f.open_source g.open_source) âŸ¨hfSource, hgSourceâŸ©

  let U : Set E := (f') '' Uâ‚€
  have hUf'target : U âŠ† f'.target := by
    rw [â† f'.image_source_eq_target]
    apply image_mono
    simp [f']
    exact inter_subset_left
  have hf'MapsToU : MapsTo f'.symm U Uâ‚€ := by
    intro _ âŸ¨_, _, hzyâŸ©
    subst hzy
    simp_all [f', Uâ‚€]

  let g' : PartialEquiv M E := g.toPartialEquiv.trans I.toPartialEquiv
  have hg'Cont : ContinuousOn g' Uâ‚€ := by
    apply ContinuousOn.mono ?_ <| show Uâ‚€ âŠ† g.source by exact inter_subset_right
    apply ContinuousOn.comp ?_ g.continuousOn g.mapsTo
    apply ContinuousOn.mono ?_ (fun _ _ â†¦ trivial)
    exact continuousOn_univ.mpr <| I.continuous

  let Ï† : PartialEquiv E E := f'.symm.trans g'
  have hÏ†Cont : ContinuousOn Ï† U := by
    simp only [PartialEquiv.coe_trans, Ï†]
    apply ContinuousOn.comp (g := g') hg'Cont ?_ hf'MapsToU
    exact ContinuousOn.mono hf'symmCont hUf'target
  rw [â† show Ï† (I (f x)) = I (g x)
      by simp [Ï†, f', g', PartialHomeomorph.left_inv f hfSource]]
  have hUÏ†source : U âŠ† Ï†.source := by
    rw [PartialEquiv.trans_source]
    simp only [PartialEquiv.symm_source, subset_inter_iff]
    simp_all [f', U, Uâ‚€, g', Ï†]
    exact hf'MapsToU.2

  have hUNhds : U âˆˆ nhds (I (f x)) := by
    apply nhds_of_nhdsWithin_of_nhds hfInterior
    rw [show U = I '' (f '' Uâ‚€) by rw [â† image_comp I f Uâ‚€]; rfl]
    apply image_mem_nhdsWithin I
    exact PartialHomeomorph.image_mem_nhds f hfSource hUâ‚€nhds

  let Ï†' : PartialEquiv E E := Ï†.restr U
  have hÏ†Cont'U : ContinuousOn Ï†' U := by simp [Ï†']; exact hÏ†Cont
  have hÏ†'Cont : ContinuousOn Ï†' Ï†'.source := by
    rw [PartialEquiv.restr_source Ï† U]
    exact ContinuousOn.mono hÏ†Cont'U inter_subset_right

  have : U âŠ† Ï†'.source := by
    rw [PartialEquiv.restr_source]
    exact subset_inter hUÏ†source fun _ a â†¦ a
  obtain hMapsNhds := maps_nhds_to_nhds E hÏ†'Cont hUNhds this
  have : (Ï†') '' U âŠ† range I := by
    rintro _ âŸ¨z, _, hzyâŸ©
    rw [â† hzy]
    exact mem_range_self (g (f'.symm z))
  exact Filter.mem_of_superset hMapsNhds this

variable (I) in
/-- A point lies in the interior of M iff *any* `PartialHomeomorph M H`
sends it to the interior of `range I`. -/
theorem isInteriorPoint_iff_any_chart {x : M} [HasInvarianceOfDomain E]
    {f : PartialHomeomorph M H} (hfSource : x âˆˆ f.source) :
    I.IsInteriorPoint x â†” I (f x) âˆˆ interior (range I) := by
  constructor <;> intro hx
  Â· apply isInteriorPoint_iff.mp at hx
    exact (independence_of_interior I M (ChartedSpace.mem_chart_source x) hfSource)
          (by exact (interior_mono <| extChartAt_target_subset_range x) hx)
  Â· apply isInteriorPoint_iff.mpr
    apply (chartAt H x).mem_interior_extend_target
          ((chartAt H x).map_source <| ChartedSpace.mem_chart_source x)
    exact independence_of_interior I M hfSource (ChartedSpace.mem_chart_source x) hx

variable (I) in
/-- A point lies on the boundary of M iff *any* `PartialHomeomorph M H`
sends it to the frontier of `range I`. -/
theorem isBoundaryPoint_iff_any_chart {x : M} [HasInvarianceOfDomain E]
    {f : PartialHomeomorph M H} (hfSource : x âˆˆ f.source) :
    (I.IsBoundaryPoint x â†” I (f x) âˆˆ frontier (range I)) := by
  apply not_iff_not.mp
  apply Iff.trans <| Iff.symm <| I.isInteriorPoint_iff_not_isBoundaryPoint x
  obtain hInt := I.isInteriorPoint_iff_any_chart hfSource
  rw [â† self_diff_frontier] at hInt
  constructor <;> intro hx
  Â· exact notMem_of_mem_diff <| hInt.mp hx
  Â· exact hInt.mpr <| mem_diff_of_mem (mem_range_self (f x)) hx

variable (I) (M) in
/-- Given any `f : PartialHomeomorph M H`, if we restrict to `f.source` then the
preimage under I âˆ˜ f of the interior of `range I` is the interior of `M`. -/
theorem preimage_chart_interior [HasInvarianceOfDomain E] (f : PartialHomeomorph M H) :
    f.source âˆ© (f.extend I) â»Â¹' (interior (range I)) = f.source âˆ© I.interior M := by
  apply Subset.antisymm_iff.mpr
  constructor <;> rintro t âŸ¨htSource, htInteriorâŸ©
  Â· apply mem_inter htSource
    apply mem_preimage.mp at htInterior
    by_contra h
    have : t âˆˆ I.boundary M := by
      by_contra hNotBoundary
      exact h <| (I.isInteriorPoint_iff_not_isBoundaryPoint t).mpr hNotBoundary
    obtain htFrontier := (I.isBoundaryPoint_iff_any_chart htSource).mp this
    exact show (f.extend I) t âˆˆ (âˆ… : Set E) by
      rw [â† Disjoint.inter_eq disjoint_interior_frontier]
      exact mem_inter htInterior htFrontier
  Â· apply mem_inter htSource
    simp only [mem_preimage]
    exact (I.isInteriorPoint_iff_any_chart htSource).mp htInterior

variable (I) (M) in
/-- Any `f : PartialHomeomorph M H` sends the interior of `M` to the interior of `range I`. -/
theorem image_chart_interior [HasInvarianceOfDomain E] (f : PartialHomeomorph M H) :
    (f.extend I) '' (f.source âˆ© (I.interior M)) âŠ† interior (range I) := by
  rw [â† I.preimage_chart_interior M]
  exact image_subset_iff.mpr inter_subset_right

variable (I) (M) in
/-- Any `f : PartialHomeomorph M H` maps the interior of `M` to the interior of `range I`. -/
theorem mapsTo_chart_interior [HasInvarianceOfDomain E] (f : PartialHomeomorph M H) :
    MapsTo (f.extend I) (f.source âˆ© I.interior M) (interior (range I)) := by
  exact fun _ ht â†¦ I.image_chart_interior M f <| mem_image_of_mem (f.extend I) ht

variable (I) (M) in
/-- Given any `f : PartialHomeomorph M H`, if we restrict to `f.source` then the
preimage under I âˆ˜ f of the frontier of `range I` is the boundary of `M`. -/
theorem preimage_chart_frontier [HasInvarianceOfDomain E] (f : PartialHomeomorph M H) :
    f.source âˆ© (f.extend I) â»Â¹' (frontier (range I)) = f.source âˆ© I.boundary M := by
  apply Subset.antisymm_iff.mpr
  constructor <;> rintro t âŸ¨htSource, htFrontierâŸ©
  Â· apply mem_preimage.mp at htFrontier
    exact mem_inter htSource <| (I.isBoundaryPoint_iff_any_chart htSource).mpr htFrontier
  Â· apply mem_inter htSource
    simp only [mem_preimage]
    exact (I.isBoundaryPoint_iff_any_chart htSource).mp htFrontier

variable (I) (M) in
/-- Any `f : PartialHomeomorph M H` sends the boundary of `M` to the frontier of `range I`. -/
theorem image_chart_boundary [HasInvarianceOfDomain E] (f : PartialHomeomorph M H) :
    (f.extend I) '' (f.source âˆ© I.boundary M) âŠ† frontier (range I) := by
  rw [â† I.preimage_chart_frontier M f]
  exact image_subset_iff.mpr inter_subset_right

variable (I) (M) in
/-- Any `f : PartialHomeomorph M H` maps the boundary of `M` to the frontier of `range I`. -/
theorem mapsTo_chart_boundary [HasInvarianceOfDomain E] (f : PartialHomeomorph M H) :
    MapsTo (f.extend I) (f.source âˆ© I.boundary M) (frontier (range I)) := by
  exact fun _ ht â†¦ (I.image_chart_boundary M f) (mem_image_of_mem (f.extend I) ht)

variable (I) (M) in
/-- The interior of `M` is an open set. -/
theorem isOpen_interior [HasInvarianceOfDomain E] : IsOpen (I.interior M) := by
  apply isOpen_iff_forall_mem_open.mpr
  intro x hx
  apply I.isInteriorPoint_iff.mp at hx
  let Ï† := extChartAt I x
  obtain âŸ¨s, hsTarget, hsOpen, hxsâŸ© := by exact mem_interior.mp hx

  haveI : Ï†.symm '' s âŠ† I.interior M := by
    rw [PartialEquiv.symm_image_eq_source_inter_preimage Ï† hsTarget]
    apply preimage_subset_iff.mpr
    rintro a âŸ¨haSource, haÏˆInvâŸ©
    rw [extChartAt_source] at haSource
    have : Ï† a âˆˆ interior (range I) := by
      apply interior_mono <| extChartAt_target_subset_range x
      apply interior_mono hsTarget
      rwa [interior_eq_iff_isOpen.mpr hsOpen]
    exact (I.isInteriorPoint_iff_any_chart haSource).mpr this

  haveI : IsOpen (Ï†.symm '' s) := by
    rw [PartialEquiv.symm_image_eq_source_inter_preimage Ï† hsTarget]
    exact ContinuousOn.isOpen_inter_preimage
      (continuousOn_extChartAt x) (isOpen_extChartAt_source x) hsOpen

  haveI : x âˆˆ Ï†.symm '' s := by
    rw [â† extChartAt_to_inv (I := I) x]
    exact mem_image_of_mem Ï†.symm hxs

  use (Ï†.symm '' s)

variable (I) (M) in
/-- The interior of `M` is a dense set. -/
theorem dense_interior [HasInvarianceOfDomain E] : Dense (I.interior M) := by
  intro x
  by_cases hx : x âˆˆ I.interior M
  Â· exact subset_closure hx
  Â· apply mem_closure_iff.mpr
    by_contra! h
    obtain âŸ¨U, hUOpen, hxU, hUintâŸ© := h
    replace hUint : U âŠ† I.boundary M := by
      rw [â† I.compl_interior]
      exact Disjoint.subset_compl_right <| disjoint_iff_inter_eq_empty.mpr hUint
    let Ï† : PartialHomeomorph M H := chartAt H x
    let V : Set M := U âˆ© Ï†.source
    let W : Set E := I '' (Ï† '' V)
    have : V âˆˆ nhds x := by
      apply (IsOpen.mem_nhds_iff <| IsOpen.inter hUOpen Ï†.open_source).mpr
      exact mem_inter hxU <| ChartedSpace.mem_chart_source x
    have hWNhd : W âˆˆ nhdsWithin (I (Ï† x)) (range I) := by
      apply image_mem_nhdsWithin I
      refine Ï†.image_mem_nhds ?_ this
      exact ChartedSpace.mem_chart_source x
    have hFrontier : frontier (range I) âˆˆ nhdsWithin (I (Ï† x)) (range I) := by
      apply Filter.mem_of_superset hWNhd ?_
      rw [show W = (Ï†.extend I) '' V by simp; exact image_image I Ï† V]
      intro y âŸ¨x',hx',hx'yâŸ©
      rw [â† hx'y, show (Ï†.extend I) x' = I (Ï† x') by rfl]
      apply (I.isBoundaryPoint_iff_any_chart (inter_subset_right hx')).mp
      exact hUint <| mem_of_mem_inter_left hx'
    obtain âŸ¨A, hAOpen, hIÏ†xA, hAintRangeâŸ© := mem_nhdsWithin.mp hFrontier

    have hIÏ†xClosure : I (Ï† x) âˆˆ closure (interior (range â†‘I)) := by
      rw [â† I.range_eq_closure_interior]
      exact mem_range_self (Ï† x)
    obtain hAIntNonempty := mem_closure_iff.mp hIÏ†xClosure A hAOpen hIÏ†xA
    let q : E := hAIntNonempty.some
    obtain âŸ¨hqA, hqIntâŸ© := (mem_inter_iff q A (interior (range I))).mp
                           <| Nonempty.some_mem hAIntNonempty
    have : q âˆˆ (interior (range I))á¶œ := by
      exact mem_of_mem_inter_right <| hAintRange
            <| mem_inter hqA <| interior_subset hqInt
    exact this hqInt

variable (I) (M) in
/-- If `M` is nonempty, then so is its interior. -/
theorem isNonempty_interior [HasInvarianceOfDomain E] :
    Nonempty M â†’ Nonempty (I.interior M) := by
  exact fun hM â†¦ Nonempty.to_subtype <| (Dense.nonempty_iff (I.dense_interior M)).mpr hM

variable (I) (M) in
/-- The boundary of `M` is a closed set. -/
theorem isClosed_boundary [HasInvarianceOfDomain E] : IsClosed (I.boundary M) := by
  apply isOpen_compl_iff.mp
  rw [ModelWithCorners.compl_boundary]
  exact I.isOpen_interior M

variable (I) (M) in
/-- The boundary of `M` is nowhere dense. -/
theorem isNowhereDense_boundary [HasInvarianceOfDomain E] :
    IsNowhereDense (I.boundary M) := by
  obtain hOpenCompl := I.isOpen_interior M
  obtain hDenseCompl := I.dense_interior M
  rw [â† I.compl_boundary] at hOpenCompl hDenseCompl
  exact (isClosed_isNowhereDense_iff_compl.mpr âŸ¨hOpenCompl, hDenseComplâŸ©).2

variable (I) in
/-- A homeomorphism between manifolds sends the interior of one to the interior of the other. -/
theorem homeomorph_image_interior [HasInvarianceOfDomain E] {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [ChartedSpace H X] [ChartedSpace H Y]
    (Ï† : X â‰ƒâ‚œ Y) : Ï† '' (I.interior X) = I.interior Y := by
  ext y
  constructor
  Â· rintro âŸ¨x, hxInt, hxyâŸ©
    let Ïˆ : PartialHomeomorph Y H := Ï†.symm.toPartialHomeomorph.trans (chartAt H x)
    have hySource : y âˆˆ Ïˆ.source := by subst hxy; simp_all [Ïˆ]
    apply (I.isInteriorPoint_iff_any_chart hySource).mpr
    rw [show Ïˆ y = (chartAt H x) x by simp [Ïˆ, â† hxy]]
    exact (I.isInteriorPoint_iff_any_chart <| ChartedSpace.mem_chart_source x).mp hxInt
  Â· intro hy
    let x : X := Ï†.symm y
    let Ïˆ : PartialHomeomorph Y H := Ï†.symm.toPartialHomeomorph.trans (chartAt H x)
    have hySource : y âˆˆ Ïˆ.source := by simp [Ïˆ, x]
    have : I (Ïˆ y) âˆˆ interior (range I) := by
      exact (I.isInteriorPoint_iff_any_chart hySource).mp hy
    rw [show Ïˆ y = (chartAt H x) x by simp [Ïˆ, x]] at this
    rw [â† Ï†.apply_symm_apply y]
    exact mem_image_of_mem Ï† this

variable (I) in
/-- A homeomorphism between manifolds sends the boundary of one to the boundary of the other. -/
theorem homeomorph_image_boundary [HasInvarianceOfDomain E] {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [ChartedSpace H X] [ChartedSpace H Y]
    (Ï† : X â‰ƒâ‚œ Y) : Ï† '' (I.boundary X) = I.boundary Y := by
  rw [â† compl_interior, â† compl_interior, image_compl_eq Ï†.bijective]
  rw [I.homeomorph_image_interior Ï†]

private def homeomorph_restrict {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [ChartedSpace H X] [ChartedSpace H Y]
    {A : Set X} {B : Set Y} (Ï† : X â‰ƒâ‚œ Y) (hÏ† : Ï† '' A = B) : A â‰ƒâ‚œ B :=
  have hÏ†image (p : A) : Ï† p.val âˆˆ B := by
    rw [â† hÏ†]
    exact mem_image_of_mem Ï† <| Subtype.coe_prop p
  let i : A â†’ B := fun p â†¦ âŸ¨Ï† p, hÏ†image pâŸ©
  have hiCont : Continuous i := by
    apply Continuous.subtype_mk ?_ hÏ†image
    exact Continuous.comp' (Homeomorph.continuous Ï†) continuous_subtype_val

  have hÏ†symm (p : B) : Ï†.symm p.val âˆˆ A := by
    have : Ï† (Ï†.symm p.val) âˆˆ Ï† '' A := by
      rw [Ï†.apply_symm_apply p, hÏ†]
      exact Subtype.coe_prop p
    obtain âŸ¨x, hxA, hÏ†âŸ© := this
    rwa [Ï†.injective hÏ†] at hxA
  let j : B â†’ A := fun p â†¦ âŸ¨Ï†.symm p, hÏ†symm pâŸ©
  have hjCont : Continuous j := by
    apply Continuous.subtype_mk ?_ hÏ†symm
    exact Continuous.comp' (Homeomorph.continuous_symm Ï†) continuous_subtype_val

  {
    toFun := i,
    invFun := j,
    left_inv := by
      intro x; simp only [Homeomorph.symm_apply_apply, Subtype.coe_eta, j, i],
    right_inv := by
      intro x; simp only [Homeomorph.apply_symm_apply, Subtype.coe_eta, j, i],
    continuous_toFun := hiCont,
    continuous_invFun := hjCont
  }

variable (I) in
/-- A homeomorphism between manifolds induces a homeomorphism between their boundaries. -/
def homeomorph_boundary [HasInvarianceOfDomain E] {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [ChartedSpace H X] [ChartedSpace H Y]
    (Ï† : X â‰ƒâ‚œ Y) : I.boundary X â‰ƒâ‚œ I.boundary Y := by
  exact homeomorph_restrict (H := H) Ï† (I.homeomorph_image_boundary Ï†)

variable (I) in
lemma homeomorph_boundary_apply [HasInvarianceOfDomain E]
  {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
  [ChartedSpace H X] [ChartedSpace H Y] (Ï† : X â‰ƒâ‚œ Y) {x : I.boundary X} :
  (homeomorph_boundary I Ï†) x = Ï† x.val := by rfl

variable (I) in
/-- A homeomorphism between manifolds induces a homeomorphism between their interiors. -/
def homeomorph_interior [HasInvarianceOfDomain E] {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [ChartedSpace H X] [ChartedSpace H Y]
    (Ï† : X â‰ƒâ‚œ Y) : I.interior X â‰ƒâ‚œ I.interior Y := by
  exact homeomorph_restrict (H := H) Ï† (I.homeomorph_image_interior Ï†)

variable (I) in
lemma homeomorph_interior_apply [HasInvarianceOfDomain E]
  {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
  [ChartedSpace H X] [ChartedSpace H Y] (Ï† : X â‰ƒâ‚œ Y) {x : I.interior X} :
  (homeomorph_interior I Ï†) x = Ï† x.val := by rfl

variable (I) in
/-- If one manifold is homeomorphic to the interior of another, then it is a
BoundarylessManifold. -/
theorem boundaryless_homeomorph_interior [HasInvarianceOfDomain E] {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [ChartedSpace H X] [ChartedSpace H Y]
    (Ï† : X â‰ƒâ‚œ I.interior Y) : BoundarylessManifold I X := by
  apply Boundaryless.of_boundary_eq_empty
  rw [â† I.compl_interior]
  apply compl_empty_iff.mpr
  apply univ_subset_iff.mp

  by_cases hY : Nonempty Y
  Â· intro x _
    let i : C(I.interior Y, Y) :=
      (ContinuousMap.mk Subtype.val).comp
        (ContinuousMap.inclusion (t := @univ Y) (fun _ _ â†¦ trivial))
    have hiEmbed : IsOpenEmbedding i := by
      constructor
      Â· exact IsEmbedding.comp IsEmbedding.subtypeVal (IsEmbedding.inclusion fun _ a â†¦ a)
      Â· have : range i = I.interior Y := by
          ext y; simp_all [i]
          constructor <;> intro h
          Â· obtain âŸ¨_, _, h'âŸ© := h; rwa [â† h']
          Â· use y, h; rfl
        rw [this]
        exact I.isOpen_interior Y
    haveI : Nonempty (I.interior Y) := by exact I.isNonempty_interior Y hY
    let y : Y := (Ï† x).val
    let Ïˆ : PartialHomeomorph X H :=
      Ï†.toPartialHomeomorph.trans <| hiEmbed.toPartialHomeomorph.trans <| chartAt H y
    have hxSource : x âˆˆ Ïˆ.source := by
      simp [Ïˆ]; exact ChartedSpace.mem_chart_source y
    apply (I.isInteriorPoint_iff_any_chart hxSource).mpr
    exact Subtype.coe_prop (Ï† x)
  Â· haveI : IsEmpty Y := by exact not_nonempty_iff.mp hY
    rw [show @univ X = âˆ… by by_contra! h; exact IsEmpty.false (Ï† h.some)]
    exact empty_subset (I.interior X)

variable (I) (M) in
noncomputable instance instChartedSpaceInterior [HasInvarianceOfDomain E] :
    ChartedSpace H (I.interior M) := by
  exact TopologicalSpace.Opens.instChartedSpace âŸ¨I.interior M, I.isOpen_interior MâŸ©

variable (I) (M) in
/-- The interior of a manifold is a BoundarylessManifold. -/
instance instBoundarylessManifold_interior [HasInvarianceOfDomain E] :
    BoundarylessManifold I (I.interior M) := by
  exact I.boundaryless_homeomorph_interior <| Homeomorph.refl (I.interior M)

end ModelWithCorners
